
;
; *** RS-232 DRIVER ROUTINES ***
;
; A MINIMAL RS-232 INTERFACE IS PROVIDED.  ONLY THE SD AND RD LINES ARE
; SUPPORTED, HALF-DUPLEX. BIT RATE IS SELECTABLE.
; 




;
;RECEIVE RS232 CHARACTER. PRESERVES ALL REGISTERS EXCEPT AF.
;
;BEFORE CALLING, USE THE FILEMANAGER (RST 08) TO SET THE RS232 PARAMETERS.
;
;RETURN: Z=1, NO CHARACTER RECEIVED
;        Z=0, CHARACTER RECEIVED IN .A
;
;CALLER MUST CALL THE ROUTINE OFTEN ENOUGH TO KEEP UP WITH INCOMING DATA.
;

RX:		XOR	A
		OUT	(SIO+2),A	;SELECT RR0
		IN	A,(SIO+2)	;CHECK STATUS REGISTER
		AND	1
		RET	Z		;NO CHARACTER--RETURN TO CALLER
		IN	A,(SIO)		;GET THE CHARACTER
		PUSH	DE
		LD	E,A
		INC	E
		CP	E
		POP	DE		;SET Z=0 TO FLAG CALLER
		RET
		
;
;TRANSMIT A CHARACTER THROUGH THE RS232 INTERFACE
;
;PARAMETERS ARE SET BY THE FILEMANAGER (RST 08H).
;
;ENTRY: CHARACTER TO TRANSMIT IS PUSHED ON STACK BY "CHROUT". CANNOT
;       BE CALLED DIRECTLY.
;
;RETURN: NO REGISTERS ARE ALTERED. THE CHARACTER IS TRANSMITTED THROUGH
;THE PORT. FORMATTING IS AUTOMATIC.
;

TX:             POP     AF                      ;RECALL CHARACTER.
		PUSH    AF                      ;SAVE IT AGAIN
		XOR	A
		OUT	(SIO+2),A		;SELECT WR0/RR0
TX1:		IN	A,(SIO+2)		;READ RR0
		BIT	2,A
		JR	Z,TX1			;WAIT IF SIO IS BUSY
		POP	AF
		OUT	(SIO),A			;WRITE--TRANSMIT
		RET


;
;SET THE RS-232 PARAMETERS.  E=VALUE FOR DATA RATE, AS FOLLOWS:
;
; 00 = 50 BPS    01 = 110 BPS   02 = 300 BPS   03 = 1200 BPS  04 = 2400 BPS
; 05 = 4800 BPS  06 = 9600 BPS  07 - FF INVALID
;
; D=NUMBER OF BITS PER CHARACTER, NORMALLY 5 TO 8. NOT RANGE CHECKED.
;
; EXIT: AF DESTROYED
;


CTC_TBL:	DB	11H,12H		;45.5 BPS
		DB	18,7		;110 BPS
		DB	1,47		;300 BPS
		DB	1,12		;1200 BPS
		DB	1,6		;2400 BPS
		DB	1,3		;4800 BPS

TBL_SIO:	DB	8AH,01H		;5 BITS/CHAR
		DB	81H,0CAH	;6 BITS/CHAR
		DB	41H,0AAH	;7 BITS/CHAR
		DB	0C1H,0EAH	;8 BITS/CHAR


SET_RATE:	PUSH	DE
		PUSH	HL
		PUSH	DE		;SAVE FOR BIT/CHAR CALC.

;
;*** PROGRAM THE Z80A CTC TO PROVIDE THE RIGHT BIT-RATE CLOCK FOR SIO ***
;

		LD	HL,+CTC_TBL
		LD	A,E
		LD	(BAUDOF),A	;SAVE BIT-RATE CODE FOR USER INFO
		LD	A,D
		LD	(BITNUM),A	;SAVE # BITS/CHAR FOR USER INFO
		SLA	E		;E=E*2
		LD	D,0
		ADD	HL,DE		;OFFSET OF DIVISOR FOR BIT RATE
		LD	A,05H		;CHANNEL A, TIMER MODE, /16
		OUT	(CTC),A		;PROGRAM CHANNEL A
		LD	A,(HL)		;TIME CONSTANT FROM TABLE
		OUT	(CTC),A
		INC	HL
		LD	A,45H
		OUT	(CTC+1),A	;CHANNEL B TRIGGERS OFF CHANNEL A
		LD	A,(HL)		;2ND HALF OF TIME CONSTANT
		OUT	(CTC+1),A	;WRITE TO CTC.


;
;*** SETUP THE Z80A SIO/0 FOR THE CORRECT NUMBER OF DATA BITS & OPERATING MODE
;

		XOR	A
		OUT	(SIO+2),A	;WR0
		INC	A
		OUT	(SIO+2),A	;WR1 SELECT
		XOR	A
		OUT	(SIO+2),A	;WR1=0
		LD	A,4
		OUT	(SIO+2),A	;WR4 SELECT
		LD	A,44H
		OUT	(SIO+2),A	;1 STOP, NO PARITY, X16 CLOCK

		POP	AF		;RECALL # BITS/CHAR IN A
		SUB	5
		SLA	A		;A=CODE *2
		LD	E,A
		LD	D,0
		LD	HL,+TBL_SIO
		ADD	HL,DE		;HL==> TABLE FOR LOOKUP BITS/CHAR

		LD	A,3
		OUT	(SIO+2),A	;SELECT WR3
		LD	A,(HL)		;WR3_VALUE
		INC	HL
		OUT	(SIO+2),A
		LD	A,5
		OUT	(SIO+2),A	;SELECT WR5
		LD	A,(HL)		;WR5_VALUE
		OUT	(SIO+2),A

		POP	HL
		POP	DE
		RET

                END



